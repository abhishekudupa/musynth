symmetrictypes {
    CacheIDType : { C1, C2 };
    DataType : { D0, D1 };
}

messages {
    Get[c] foreach c in CacheIDType,
    Put[c] foreach c in CacheIDType,
    Havoc[c][d] foreach c in CacheIDType foreach d in DataType,
    GetDone[c][d] foreach c in CacheIDType foreach d in DataType,
    HavocDone[c][d] foreach c in CacheIDType foreach d in DataType,
    PutDone[c] foreach c in CacheIDType,
    Req2Dir[c] foreach c in CacheIDType,
    Req2Dir'[c] foreach c in CacheIDType,
    WB2Dir[c][d] foreach c in CacheIDType foreach d in DataType,
    WB2Dir'[c][d] foreach c in CacheIDType foreach d in DataType,
    InvAck2Dir[c][d] foreach c in CacheIDType foreach d in DataType,
    InvAck2Dir'[c][d] foreach c in CacheIDType foreach d in DataType,
    Ack2Dir[c] foreach c in CacheIDType,
    Ack2Dir'[c] foreach c in CacheIDType,
    Rsp[c][d] foreach c in CacheIDType foreach d in DataType,
    Rsp'[c][d] foreach c in CacheIDType foreach d in DataType,
    WBAck[c] foreach c in CacheIDType,
    WBAck'[c] foreach c in CacheIDType,
    Inv[c] foreach c in CacheIDType,
    Inv'[c] foreach c in CacheIDType
}

Automaton Environment[c] foreach c in CacheIDType
{
    states : { InitState[d] foreach d in DataType, 
               IssuedGet, IssuedPut, 
               IssuedHavoc[d] foreach d in DataType };
    
    inputs {
        GetDone[c][d] foreach d in DataType,
        HavocDone[c][d] foreach d in DataType,
        PutDone[c]
    };

    outputs {
        Get[c], Put[c],
        Havoc[c][d] foreach d in DataType
    };

    transitions {
        (InitState[d], Get[c], IssuedGet foreach d in DataType),
        (InitState[d], Put[c], IssuedPut foreach d in DataType),
        (InitState[d1], Havoc[c][d2], IssuedHavoc[d2] foreach d1, d2 in DataType),

        (IssuedGet, GetDone[c][d], InitState[d] foreach d in DataType),
        (IssuedPut, PutDone[c], InitState[d] foreach d in DataType),
        (IssuedHavoc[d], HavocDone[c][d], InitState[d] foreach d in DataType)
    };
}

Automaton Oracle
{
    states : { Data[d] foreach d in DataType };

    inputs {
        HavocDone[c][d] foreach c in CacheIDType foreach d in DataType
    };

    transitions {
        (Data[d1], HavocDone[c][d2], Data[d2] foreach d1, d2 in DataType 
                                              foreach c in CacheIDType)
    };
}

ChannelAutomaton ReqChannel[c] foreach c in CacheIDType
{
    unordered, lossless, nonduplicating;
    capacity = 1;    

    messages {
        Req2Dir[c], 
        WB2Dir[c][d] foreach d in DataType,
        InvAck2Dir[c][d] foreach d in DataType,
        Ack2Dir[c]
    };
}

ChannelAutomaton RspChannel[c] foreach c in CacheIDType
{
    unordered, lossless, nonduplicating;
    capacity = 1;

    messages {
        Rsp[c][d] foreach d in DataType,
        WBAck[c],
        Inv[c]
    };
}

PartialAutomaton Cache[c] foreach c in CacheIDType
{
    states : { CI : incomplete, 
               CIV1 : complete,
               CIV2 : complete,
               CIV3[d] foreach d in DataType : complete,
               CIV4[d] foreach d in DataType : complete,
               CIVH1[d] foreach d in DataType : complete,
               CIVH2[d] foreach d in DataType : complete,
               CIVH3[d] foreach d in DataType : complete,
               CIVH4[d] foreach d in DataType : complete,
               CV[d] foreach d in DataType : complete, 
               CVP[d] foreach d in DataType : complete,
               CVH[d] foreach d in DataType : complete,
               CVI1[d] foreach d in DataType : complete,
               CVI2 : incomplete, 
               CVI3 : complete,
               CVI4[d] foreach d in DataType : complete };

    inputs {
        Get[c], Put[c], 
        Havoc[c][d] foreach d in DataType,
        Rsp'[c][d] foreach d in DataType,
        WBAck'[c], Inv'[c]
    };

    outputs {
        Req2Dir[c], 
        WB2Dir[c][d] foreach d in DataType, 
        InvAck2Dir[c][d] foreach d in DataType,
        Ack2Dir[c],
        GetDone[c][d] foreach d in DataType,
        PutDone[c],
        HavocDone[c][d] foreach d in DataType
    };

    transitions {
        (CI, Get[c], CIV1),
        (CIV1, Req2Dir[c], CIV2),
        (CIV2, Rsp'[c][d], CIV3[d] foreach d in DataType),
        (CIV3[d], Ack2Dir[c], CIV4[d] foreach d in DataType),
        (CIV4[d], GetDone[c][d], CV[d] foreach d in DataType),

        (CI, Havoc[c][d], CIVH1[d] foreach d in DataType),
        (CIVH1[d], Req2Dir[c], CIVH2[d] foreach d in DataType),
        (CIVH2[d1], Rsp'[c][d2], CIVH3[d1] foreach d1, d2 in DataType),
        (CIVH3[d], Ack2Dir[c], CIVH4[d] foreach d in DataType),
        (CIVH4[d], HavocDone[c][d], CV[d] foreach d in DataType),

        (CV[d], Get[c], CVP[d] foreach d in DataType),
        (CVP[d], GetDone[c][d], CV[d] foreach d in DataType),
        (CV[d1], Havoc[c][d2], CVH[d2] foreach d1, d2 in DataType),
        (CVH[d], HavocDone[c][d], CV[d] foreach d in DataType),

        (CV[d], Put[c], CVI1[d] foreach d in DataType),
        (CVI1[d], WB2Dir[c][d], CVI2 foreach d in DataType),
        (CVI2, WBAck'[c], CVI3),
        (CVI3, PutDone[c], CI),

        (CV[d], Inv'[c], CVI4[d] foreach d in DataType),
        (CVI4[d], InvAck2Dir[c][d], CI foreach d in DataType)
        
        /*
        (CVI2, Inv'[c], CI)
        */
    };
}

PartialAutomaton Directory
{
    states : { DI[d] foreach d in DataType : complete,
               DIV1[c][d] foreach c in CacheIDType foreach d in DataType : complete,
               DIV2[c] foreach c in CacheIDType : complete,
               DV[c] foreach c in CacheIDType : complete,
               DVV1[c1][c2] foreach c1, c2 in CacheIDType (!= c1 c2) : complete,
               DVV2[c1][c2] foreach c1, c2 in CacheIDType (!= c1 c2) : incomplete,
               DVV3[c][d] foreach c in CacheIDType foreach d in DataType : complete,
               DVV4[c] foreach c in CacheIDType : complete,
               DVI1[c][d] foreach c in CacheIDType foreach d in DataType : complete };

    inputs {
        Req2Dir'[c] foreach c in CacheIDType,
        WB2Dir'[c][d] foreach c in CacheIDType foreach d in DataType,
        InvAck2Dir'[c][d] foreach c in CacheIDType foreach d in DataType,
        Ack2Dir'[c] foreach c in CacheIDType
    };

    outputs {
        Rsp[c][d] foreach c in CacheIDType foreach d in DataType,
        WBAck[c] foreach c in CacheIDType,
        Inv[c] foreach c in CacheIDType
    };

    transitions {
        (DI[d], Req2Dir'[c], DIV1[c][d] foreach d in DataType foreach c in CacheIDType),
        (DIV1[c][d], Rsp[c][d], DIV2[c] foreach d in DataType foreach c in CacheIDType),
        (DIV2[c], Ack2Dir'[c], DV[c] foreach c in CacheIDType),
        
        (DV[c1], Req2Dir'[c2], DVV1[c1][c2] foreach c1, c2 in CacheIDType (!= c1 c2)),
        (DVV1[c1][c2], Inv[c1], DVV2[c1][c2] foreach c1, c2 in CacheIDType (!= c1 c2)),
        (DVV2[c1][c2], InvAck2Dir'[c1][d], DVV3[c2][d] 
         foreach c1, c2 in CacheIDType 
         foreach d in DataType (!= c1 c2)),
        (DVV3[c][d], Rsp[c][d], DVV4[c] foreach c in CacheIDType foreach d in DataType),
        (DVV4[c], Ack2Dir'[c], DV[c] foreach c in CacheIDType),

        (DV[c], WB2Dir'[c][d], DVI1[c][d] foreach c in CacheIDType foreach d in DataType),
        (DVI1[c][d], WBAck[c], DI[d] foreach c in CacheIDType foreach d in DataType)
        
        /*
        (DVV2[c1][c2], WB2Dir'[c1][d], DVV3[c2][d] foreach c1, c2 in CacheIDType foreach d in DataType (c1 != c2))
        */
    };
}

init {
    Environment[c].state = InitState[d] foreach c in CacheIDType foreach d in DataType;
    Oracle.state = Data[d], Directory.state = DI[d] foreach d in DataType;
    Cache[c].state = CI foreach c in CacheIDType;
}

invariant "Coherence Invariant" {
    (forall d in DataType 
     (forall c in CacheIDType
      (-> (= Cache[c].state CV[d]) (= Oracle.state Data[d]))))
}

invariant "One Valid Invariant" {
    (forall c1 in CacheIDType
     (forall c2 in CacheIDType
      (forall d1 in DataType
       (forall d2 in DataType
        (-> (!= c1 c2) (-> (= Cache[c1].state CV[d1]) 
                           (!= Cache[c2].state CV[d2])))))))
}

ltlspec "I-V liveness on Get" {
    (forall c in CacheIDType
     (exists d in DataType
      (G (-> (= Environment[c].state IssuedGet) (F (and (= Environment[c].state InitState[d]) (= Oracle.state Data[d])))))))
}

ltlspec "I-V liveness on Havoc" {
    (forall c in CacheIDType
     (forall d in DataType
      (G (-> (= Environment[c].state IssuedHavoc[d]) (F (and (= Environment[c].state InitState[d]) (= Oracle.state Data[d])))))))
}

ltlspec "V-I liveness" {
    (forall c in CacheIDType
     (exists d in DataType
      (G (-> (= Environment[c].state IssuedPut) (F (= Environment[c].state InitState[d]))))))
}
